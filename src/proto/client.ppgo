import (
    "fiber";
    "io";
)

class Conn
{
    conn fiber.Conn;

    func init(conn fiber.Conn)
    {
        this.conn = conn;
    }

    public func read(b [:]byte) int
    {
        return this.conn.read(b);
    }

    public func write(b [:]byte)
    {
        fiber.Ctx(timeout = 1.0).call(func () {
            this.conn.write(b);
        });
    }

    public func write_str(s string)
    {
        fiber.Ctx(timeout = 1.0).call(func () {
            this.conn.write_str(s);
        });
    }
}

public class Client
{
    conn Conn;
    br io.BufReader;
    bw io.BufWriter;

    public func init(conn fiber.Conn)
    {
        this.conn = Conn(conn);
        this.br = io.BufReader(this.conn);
        this.bw = io.BufWriter(this.conn);
    }

    public func byte_count_read() int
    {
        return this.br.byte_count_read();
    }
    public func byte_count_written() int
    {
        return this.bw.byte_count_written();
    }

    func recv_int() (prefix byte, v int)
    {
        var b []byte;
        b.resize(32);
        var n = this.br.read_until('\n', b);
        if (n >= 4 && b[n - 2] == '\r' && b[n - 1] == '\n')
        {
            prefix = b[0];
            var (
                s = b[1 : n - 2].<string>;
                exc any;
            )
            v, exc = s.parse_int(base = 10);
            if (exc == nil)
            {
                return;
            }
        }
        throw("invalid integer in proto %s".(b.<string>.repr()));
    }

    public func recv_req() (req []string)
    {
        var n = this.br.wait();
        if (n == 0)
        {
            //eof
            return;
        }
        var prefix, v = this.recv_int();
        var item_count = v;
        if (!(prefix == '*' && item_count > 0))
        {
            throw("invalid req: list len error");
        }
        req.resize(item_count);
        for (var i : 0 .. item_count)
        {
            prefix, v = this.recv_int();
            if (!(prefix == '$' && v >= 0))
            {
                throw("invalid req: str len error");
            }

            var b []byte;
            b.resize(v + 2);
            this.br.read_full(b);
            if (!(b[v] == '\r' && b[v + 1] == '\n'))
            {
                throw("invalid req: str end CRLF error");
            }
            req[i] = b[: v].<string>;
        }
        req[0] = req[0].upper();
    }

    func replace_crlf(mb [:]byte)
    {
        for (var i : 0 .. mb.len())
        {
            if (mb[i] == '\r' || mb[i] == '\n')
            {
                mb[i] = '\x20';
            }
        }
    }

    public func send_ok(/, msg string)
    {
        if (msg.valid())
        {
            var b = []byte{'+'};
            b.extend(msg.get().<[]byte>);
            this.replace_crlf(b);
            b.append('\r');
            b.append('\n');
            this.bw.write(b);
        }
        else
        {
            this.bw.write_str("+OK\r\n");
        }
    }

    public func send_err(msg string, /, type string)
    {
        var b = []byte{'-'};
        b.extend(type.get_or("ERR").<[]byte>);
        b.append('\x20');
        b.extend(msg.<[]byte>);
        this.replace_crlf(b);
        b.append('\r');
        b.append('\n');
        this.bw.write(b);
    }

    public func send_int(n int)
    {
        this.bw.write_str(":%d\r\n".(n));
    }

    public func send_nil()
    {
        this.bw.write_str("$-1\r\n");
    }

    public func send_str(s string)
    {
        this.bw.write_str("$%d\r\n".(s.len()));
        this.bw.write_str(s);
        this.bw.write_str("\r\n");
    }

    public func send_list_len(len int)
    {
        this.bw.write_str("*%d\r\n".(len));
    }

    public func send_nil_list()
    {
        this.send_list_len(-1);
    }

    public func flush()
    {
        this.bw.flush();
    }
}
